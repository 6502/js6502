<!DOCTYPE html>
<!--
 ****************************************************************************
******************************************************************************
**                                                                          **
**  Copyright (c) 2012 by Andrea Griffini                                   **
**                                                                          **
**  Permission is hereby granted, free of charge, to any person obtaining   **
**  a copy of this software and associated documentation files (the         **
**  "Software"), to deal in the Software without restriction, including     **
**  without limitation the rights to use, copy, modify, merge, publish,     **
**  distribute, sublicense, and/or sell copies of the Software, and to      **
**  permit persons to whom the Software is furnished to do so, subject to   **
**  the following conditions:                                               **
**                                                                          **
**  The above copyright notice and this permission notice shall be          **
**  included in all copies or substantial portions of the Software.         **
**                                                                          **
**  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         **
**  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      **
**  MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND                   **
**  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE  **
**  LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION  **
**  OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION   **
**  WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.         **
**                                                                          **
******************************************************************************
 ****************************************************************************
-->
<html>
  <head>
    <title>6502 emulator</title>
  </head>
  <body>
    Virtual graphic screen<br/>
    <canvas id="canvas" style="width:512px; height:512px; border:solid 1px #000">
    </canvas>
    <textarea id="source" style="height:508px; width:512px;">
;
; Special locations
; (they work with LDA/STA abs only!)
;
set_palette = $FF00 ; W: sets current index
write_color = $FF01 ; W++: R, G or B
set_row     = $FF02 ; W: pixel row
set_col     = $FF03 ; W: pixel col
write_pixel = $FF04 ; W++: writes a pixel

random      = $FF80 ; R: a random byte
clock0      = $FF81 ; R: updates clock and returns low byte
clock1      = $FF82 ; R: returns second byte of clock
clock2      = $FF83 ; R: returns third byte of clock
clock3      = $FF84 ; R: returns fourth byte of clock

;
; Execution begins at "start" and stops when
; program counter becomes $0000
;
    .org $0200

;; Box drawing parameters
x0    = $00
y0    = $01
x1    = $02
y1    = $03
color = $04

;; Animated square position and speed
sqx   = $05
sqy   = $06
sqdx  = $07
sqdy  = $08

;; Line drawing
dx    = $09
dy    = $0A
ix    = $0B
iy    = $0C
cx    = $0D
cy    = $0E
m     = $0F

;; Draws a line from x0, y0 to x1, y1
drawline:
    ; dx/ix computation
    ldx #$01
    lda x1
    sec
    sbc x0
    bcs dlxpos
    eor #$FF
    sbc #$FE
    ldx #$FF
dlxpos:
    sta dx
    stx ix

    ; dy/iy computation
    ldx #$01
    lda y1
    sec
    sbc y0
    bcs dlypos
    eor #$FF
    sbc #$FE
    ldx #$FF
dlypos:
    sta dy
    stx iy

    ; m/cx/cy computation
    lda dx
    cmp dy
    bcs dlxm
    lda dy
dlxm:
    sta m
    tay
    bne dlsome
    rts
dlsome:
    lsr a
    sta cx
    sta cy

    ; main loop
dlloop:
    lda x0
    sta set_col
    lda y0
    sta set_row
    lda color
    sta write_pixel

    ; x-inc
    lda cx
    clc
    adc dx
    sta cx
    bcs dlxi
    cmp m
    bcc dlnoxi
dlxi:
    lda x0
    clc
    adc ix
    sta x0
    lda cx
    sec
    sbc m
    sta cx
dlnoxi:

    ; y-inc
    lda cy
    clc
    adc dy
    sta cy
    bcs dlyi
    cmp m
    bcc dlnoyi
dlyi:
    lda y0
    clc
    adc iy
    sta y0
    lda cy
    sec
    sbc m
    sta cy
dlnoyi:

    dey
    bne dlloop
    rts


;; Fills a box with a solid color
fillbox:
    lda y0
    sta set_row
    lda x0
    sta set_col
    lda x1
    sec
    sbc x0
    tax
    lda color
fbxloop:
    sta write_pixel
    dex
    bne fbxloop
    inc y0
    lda y0
    cmp y1
    bne fillbox
    rts

;; Sets square coordinates
set_square:
    lda sqx
    sta x0
    clc
    adc #$20
    sta x1
    lda sqy
    sta y0
    clc
    adc #$20
    sta y1
    rts

;; Main program
start:
    ; clear screen to random
    lda #$00
    tax
    tay
cloop:
    lda random
    sta write_pixel
    dex
    bne cloop
    dey
    bne cloop

    ; draw 2048 random lines
    ldy #$08
    ldx #$00
cloop2:
    txa
    pha
    tya
    pha
    lda random
    sta x1
    lda random
    sta y1
    tya
    sta color
    jsr drawline
    pla
    tay
    pla
    tax
    dex
    bne cloop2
    dey
    bne cloop2

    ; Set a green-red bouncing palette
    lda #$00
    sta set_palette
    ldx #$00
ploop1:
    txa
    sta write_color
    eor #$FF
    sta write_color
    lda #$00
    sta write_color
    inx
    inx
    bne ploop1
ploop2:
    txa
    eor #$FF
    sta write_color
    eor #$FF
    sta write_color
    lda #$00
    sta write_color
    inx
    inx
    bne ploop2

    ; Set starting position and speed
    lda #$01
    sta sqx
    sta sqy
    sta sqdx
    sta sqdy
mainloop:
    ; Increment square position
    lda sqx
    clc
    adc sqdx
    cmp #$DF
    bcc xok
    lda sqdx
    eor #$FF
    sta sqdx
    inc sqdx
    lda sqx
xok:
    sta sqx
    lda sqy
    clc
    adc sqdy
    cmp #$DD
    bcc yok
    lda sqdy
    eor #$FF
    sta sqdy
    inc sqdy
    lda sqy
yok:
    sta sqy

    ; Draw square
    jsr set_square
    inc color
    jsr fillbox

    jmp mainloop

    </textarea>
    <input type=button value="Run" onclick="doit()">
    <script src="6502.js"></script>
    <script>

// Virtual screen interface /////////////////////////////////////////////////////

var canvas = document.getElementById("canvas");
canvas.width = canvas.height = 256;
var ctx = canvas.getContext("2d");
var data = ctx.getImageData(0, 0, 256, 256);
var bytes = data.data;
var current_palette = [];
var current_palette_index = 0;
var current_palette_comp = 0;
var current_address = 0;

(function(){
    for (var r=0; r<6; r++)
        for (var g=0; g<6; g++)
            for (var b=0; b<6; b++)
                current_palette.push([r*51, g*51, b*51]);
    for (var g=0; g<40; g++)
        current_palette.push([Math.floor(g*255/39+0.5),
                              Math.floor(g*255/39+0.5),
                              Math.floor(g*255/39+0.5)]);
})();

function set_palette(x) { current_palette_index = x; current_palette_comp = 0; }

function write_color(x)
{
    current_palette[current_palette_index][current_palette_comp] = x;
    if (++current_palette_comp == 3)
    {
        current_palette_comp = 0;
        current_palette_index = (current_palette_index + 1) & 255;
    }
}

function set_row(x) { current_address = x*256*4 + (current_address & (255*4)); }
function set_col(x) { current_address = x*4 + (current_address & (255*256*4)); }
function write_pixel(x)
{
    var c = current_palette[x];
    bytes[current_address] = c[0];
    bytes[current_address+1] = c[1];
    bytes[current_address+2] = c[2];
    current_address = (current_address + 4) & (256*256*4-1);
}

(function(){
    for (var i=0; i<256*256; i++)
        bytes[i*4+3] = 255;
    ctx.putImageData(data, 0, 0);
})();

// Random numbers and time tracking

var ctime = 0;

function random() { return (Math.random() * 256) & 255; }
function clock0() { ctime = (new Date).getTime(); return ctime&255; }
function clock1() { return (ctime >> 8) & 255; }
function clock2() { return (ctime >> 16) & 255; }
function clock3() { return (ctime >> 24) & 255; }

// Note: special read/write only works for LDA/STA absolute
special_write[0xFF00] = set_palette;
special_write[0xFF01] = write_color;
special_write[0xFF02] = set_row;
special_write[0xFF03] = set_col;
special_write[0xFF04] = write_pixel;

special_read[0xFF80] = random;
special_read[0xFF81] = clock0;
special_read[0xFF82] = clock1;
special_read[0xFF83] = clock2;
special_read[0xFF84] = clock3;

function doit()
{
    try
    {
        assemble(document.getElementById("source").value);
    }
    catch(err)
    {
        alert(err);
        return;
    }
    setTimeout(run, 0);
}

    </script>
  </body>
</html>
